(ns graph.compiler-2-runtime.program-topology
  "Program-net topology seeding for compiler-2 runtime TUI blocks."
  (:require [graph.compiler-2-runtime.program :as program]
            [propagators.datastructures.compound-object :as obj]
            [propagators.network-builder :as nb]))

(defn- seed-instance
  [runtime-net [program-net props] {:keys [instance-id blocks-id]}]
  (let [program-net (-> program-net
                        (program/seed-program-block-cell runtime-net instance-id)
                        (program/seed-program-block-cell runtime-net blocks-id))
        [prop-id program-net] ((obj/p:slot :instance/blocks
                                           blocks-id
                                           instance-id)
                               program-net)]
    [program-net (conj props prop-id)]))

(defn- seed-block
  [runtime-net [program-net props] block]
  (let [program-net (reduce #(program/seed-program-block-cell %1
                                                              runtime-net
                                                              %2)
                            program-net
                            [(:block-id block) (:index-id block)
                             (:next-id block) (:text-id block)
                             (:display-id block)])
        [index-prop program-net] ((obj/p:slot :block/index
                                              (:index-id block)
                                              (:block-id block))
                                  program-net)
        program-net (if (:text-current-source? block)
                      (nb/install-cell program-net
                                       (:text-id block)
                                       (:text-current block)
                                       (:text-current block))
                      program-net)
        [text-prop program-net] ((obj/p:slot :block/text
                                             (:text-id block)
                                             (:block-id block))
                                 program-net)
        [display-prop program-net] ((obj/p:slot :block/display
                                                (:display-id block)
                                                (:block-id block))
                                    program-net)
        [next-prop program-net] ((obj/p:cdr (:next-id block)
                                            (:block-id block))
                                 program-net)]
    [program-net (into props [index-prop text-prop display-prop next-prop])]))

(defn seed-program-topology
  [state program-net]
  (let [runtime-net (:network state)
        [program-net props] (reduce (partial seed-instance runtime-net)
                                    [program-net []]
                                    (vals (:tuis state)))
        [program-net props] (reduce (partial seed-block runtime-net)
                                    [program-net props]
                                    (program/all-blocks state))]
    (nb/run-propagators program-net props)))

(defn seed-appended-block-topology-state
  [state client-id block previous-tail]
  (let [runtime-net (:network state)
        tui (get-in state [:tuis client-id])
        [program-net props] (if tui
                              (seed-instance runtime-net
                                             [(:program/net state) []]
                                             tui)
                              [(:program/net state) []])
        [program-net props] (if previous-tail
                              (seed-block runtime-net
                                          [program-net props]
                                          previous-tail)
                              [program-net props])
        [program-net props] (seed-block runtime-net
                                        [program-net props]
                                        block)]
    (assoc state :program/net (nb/run-propagators program-net props))))
